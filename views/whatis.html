        <p>Playing with the points for curves may have given you a feel for how Bézier curves behaves, but
        what <em>are</em> Bézier curves, really?</p>

        <p>There are two ways to explain what a Bézier curve is, and they turn out to be the entirely equivalent,
        but one of them uses complicated maths, and the other uses really simple maths. So... let's start
        with the simple explanation:<p>

        <p>Bezier curves are the result of <a href="https://en.wikipedia.org/wiki/Linear_interpolation">linear
        interpolations</a>. That sounds complicated but you've been doing linear interpolation since you were
        very young: any time you had to point at something between two other things, you've been applying
        linear interpolation. It's simply "picking a point between two, points". If we call the distance
        between those two points <em>d</em>, and we want a point that is, say, 20% the distance away from
        the first point (and thus 80% the distance away from the second point) then we can compute that
        really simply:<p>

        <p>\[
          p_1 = (x_1,y_1), \\
          p_2 = (x_2,y_2), \\
          distance = (p_2 - p_1), \\
          ratio = \frac{percentage}{100}, \\
          interpolated\ point = p_1 + distance \cdot ratio
        \]</p>

        <p>So let's look at that in action: the following graphic is interactive in that you can use your
        '+' and '-' keys to increase or decrease the interpolation distance, to see what happens. We start
        with three points, which gives us two lines. Linear interpolation over those lines gives use two
        points, between which we can again perform linear interpolation, yielding a single point. And that
        point, and all points we can form in this way for all distances taken together, form our Bézier curve:</p>

        <textarea class="sketch-code" data-sketch-preset="threepanel" data-sketch-title="Linear Interpolation leading to Bézier curves">
        void setupCurve() {
          setupDefaultQuadratic();
          offsetting();
          offset = 20;
        }

        void drawCurve(BezierCurve curve) {
          if(offset>100) offset=100;
          float r = offset / 100.0;
          Point[] p = curve.points;
          curve.draw();

          float dx1 = p[1].x-p[0].x,
                dy1 = p[1].y-p[0].y,
                pr1x = p[0].x + r * dx1,
                pr1y = p[0].y + r * dy1;

          ellipse(pr1x, pr1y, 5, 5);

          float dx2 = p[2].x-p[1].x,
                dy2 = p[2].y-p[1].y,
                pr2x = p[1].x + r * dx2,
                pr2y = p[1].y + r * dy2;

          ellipse(pr2x, pr2y, 5, 5);

//          stroke(200);
//          line(pr1x, pr1y, pr2x, pr2y);

          stroke(0,0,200);
          strokeWeight(2);
          line(p[0].x, p[0].y, pr1x, pr1y);
          line(p[1].x, p[1].y, pr2x, pr2y);

          text("linear interpolation distance: " + offset + "%", 5, 15);
          text("linear interpolation between the first set of points", 5, dim-5);

        nextPanel();

          stroke(0);
          strokeWeight(1);
          line(0,0,0,dim);

          curve.draw();
          ellipse(pr1x, pr1y, 5, 5);
          ellipse(pr2x, pr2y, 5, 5);

          float dx3 = pr2x - pr1x,
                dy3 = pr2y - pr1y,
                pr3x = pr1x + r * dx3,
                pr3y = pr1y + r * dy3;

          ellipse(pr3x, pr3y, 5, 5);

          stroke(150);
          line(pr1x, pr1y, pr2x, pr2y);

          strokeWeight(4);
          stroke(0,0,200, 50);
          line(p[0].x, p[0].y, pr1x, pr1y);
          line(p[1].x, p[1].y, pr2x, pr2y);

          strokeWeight(2);
          stroke(0,0,200);
          line(pr1x, pr1y, pr3x, pr3y);

          text("same linear interpolation distance: " + offset + "%", 5, 15);
          text("linear interpolation between the second set of points", 5, dim-5);

        nextPanel();

          stroke(0);
          strokeWeight(1);
          line(0,0,0,dim);
          curve.draw();
          stroke(0);
          fill(0);
          ellipse(pr3x, pr3y, 5, 5);

          text("the second interpolation turns out to be a curve point!", 5, dim-5);

        }</textarea>

        <p>And that brings us to the complicated maths: calculus. While it doesn't look like that's what we've just done,
        we actually just drew a quadratic curve, in steps, rather than in a single go. One of the fascinating parts
        about Bézier curves is that they can both be described in terms of polynomial functions, as well as in terms
        of very simple interpolations of interpolations of [...]. That it turn means we can look at what these curves
        can do based on both "real maths" (by examining the functions, their derivatives, and all that stuff), as well
        as by looking at the "mechanical" composition (which tells us that a curve will never extend beyond the points
        we used to construct it, for instance)</p>

        <p>So let's start looking at Bézier curves a bit more in depth. Their mathematical expressions, the properties we
        can derive from those, and the various things we can do to, and with, Bézier curves.</p>
